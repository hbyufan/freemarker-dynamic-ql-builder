apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

project.version = '0.1'
project.group = 'kr.pe.kwonnam.freemarkerdynamicqlbuilder'

ext {
    javaVersion = '1.6'
    freemarkerVersion = '2.3.23'
    slf4jVersion = '1.7.13'
    logbackVersion = '1.1.3'
}

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.freemarker:freemarker:${freemarkerVersion}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    testCompile 'junit:junit:4.12'
    // DO NOT use assertj - it's available with JDK 7+. freemarker-dynamic-ql-builder should support JDK 6.
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile "ch.qos.logback:logback-classic:${logbackVersion}"
    testCompile "joda-time:joda-time:2.9.1"
    testCompile "org.joda:joda-money:0.10.0"
    testCompile 'com.h2database:h2:1.4.190'
}

jacoco {
    toolVersion = '0.7.5.201505241946'
}

test {
    ignoreFailures = true
    jacoco {
        enabled = true
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

apply from: 'gradle/upload_archives.gradle'